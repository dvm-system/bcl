#
# Copyright 2018 Nikita Kataev
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

set(BCL_DESCRIPTION "@BCL_DESCRIPTION@")
set(BCL_AUTHOR "@BCL_AUTHOR@")
set(BCL_HOMEPAGE_URL "@BCL_HOMEPAGE_URL@")

# Path to available CMake modules.
set(BCL_CMAKE_MODULE_PATH ${CMAKE_CURRENT_LIST_DIR})

# ON if legacy code is available, OFF otherwise.
set(BCL_LEGACY @BCL_LEGACY@)

# ON if tests is available, OFF otherwise.
set(BCL_TEST @BCL_TEST@)

# ON if examples is available, OFF otherwise.
set(BCL_EXAMPLE @BCL_EXAMPLE@)

# ON if NodeJSSocket is available, OFF otherwise.
set(BCL_NODEJS_SOCKET @BCL_NODEJS_SOCKET@)

# ON if CSocket is available, OFF otherwise.
set(BCL_C_SOCKET @BCL_C_SOCKET@)

if (BCL_C_SOCKET)
  # Export alias for convenience.
  add_library(BCL::CSocket ALIAS BCLCSocket)
endif()

if (EXISTS ${CMAKE_CURRENT_LIST_DIR}/BCLExports.cmake)
  # ON if BCLExports.cmake is available.
  set(BCL_EXPORT ON)

  include(${CMAKE_CURRENT_LIST_DIR}/BCLExports.cmake)

  # List of headers from the core of BCL.
  get_property(BCL_CORE_HEADERS TARGET BCL::Core PROPERTY INTERFACE_SOURCES)

  # List of sources from NodeJSSocket (if NodeJSSocket is available).
  if(BCL_NODEJS_SOCKET)
    get_property(BCL_NODEJS_SOCKET_SOURCES TARGET BCL::NodeJSSocket
      PROPERTY INTERFACE_SOURCES)
  endif()
else()
  # OFF if BCLExports.cmake is not available. For example, if add_subdirectory()
  # has been used to add this project to the build tree.
  set(BCL_EXPORT OFF)

  add_library(BCL::Core ALIAS Core)

  if(BCL_NODEJS_SOCKET)
    add_library(BCL::NodeJSSocket ALIAS NodeJSSocket)
  endif()

  # Try to set some variables which is available in BCL build tree. So, the
  # following variables will be available if add_subdirectory() has been used
  # configure this project.

  # List of headers from the core of BCL.
  set(BCL_CORE_HEADERS @BCL_CORE_HEADERS@)

  # List of sources from NodeJSSocket (if NodeJSSocket is available).
  if(BCL_NODEJS_SOCKET)
    set(BCL_NODEJS_SOCKET_SOURCES @BCL_NODEJS_SOCKET_SOURCES@)
  endif()
endif()
